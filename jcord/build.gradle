plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.mqzn'
version '1.1.2'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    implementation project(":common")
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'org.javacord:javacord:3.8.0'
    compileOnly "net.kyori:adventure-api:4.13.1"
    compileOnly "net.kyori:adventure-platform-bungeecord:4.3.0"
}

def targetJavaVersion = 17

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()

}

compileJava {
    options.encoding = "UTF-8"
}


shadowJar {
    setArchiveName("mCommands-JCord-${project.version}.jar")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'mCommands-jcord'
            version project.version
            from components.java


            pom {
                name = 'mCommands'
                description = 'Advanced command dispatching java library'
                url = 'https://github.com/Mqzn/mCommands'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'mqzn'
                        name = 'Mqzen'
                        email = 'mezoahmed2507@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/Mqzn/mCommands.git'
                    developerConnection = 'scm:git:ssh://github.com/Mqzn/mCommands.git'
                    url = 'https://github.com/Mqzn/mCommands'
                }
            }

        }

    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }

        }

    }
}

signing {
    sign publishing.publications.mavenJava
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'signing'

